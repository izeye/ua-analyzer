buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/libs-release" }
        maven { url "http://repo.spring.io/libs-milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.springframework.build.gradle:propdeps-plugin:$propdepsPluginVersion")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'propdeps'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://repo.spring.io/libs-milestone" }
    maven { url "http://repo.spring.io/snapshot" }
    mavenLocal()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.apache.commons:commons-lang3:$commonsLangVersion")

    provided("org.projectlombok:lombok")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

springBoot {
    executable = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

task coreJar(type: Jar) {
    from sourceSets.main.output
    include 'com/izeye/uaanalyzer/core/**'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

ext {
    artifactVersion = '0.1.0.RELEASE'
}

publishing {
    publications {
        core(MavenPublication) {
            groupId 'com.izeye.ua-analyzer'
            artifactId 'ua-analyzer-core'
            version artifactVersion

            artifact coreJar

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username "admin"
                password "secret123"
            }

            if (artifactVersion.endsWith("BUILD-SNAPSHOT")) {
                url "http://localhost:8080/repository/snapshots"
            }
            else {
                url "http://localhost:8080/repository/internal"
            }
        }
    }
}

checkstyle {
    toolVersion = '7.0'
    configProperties = [
            'headerLocation': 'config/checkstyle/checkstyle-header.txt',
            'suppressionsLocation': 'config/checkstyle/checkstyle-suppressions.xml'
    ]
}
